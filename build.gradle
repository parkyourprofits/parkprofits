plugins {
  id "com.craigburke.bower-installer" version "2.5.1"
  id "com.monochromeroad.s3sync" version "0.10"
  id "org.ajoberstar.github-pages" version "1.5.1-rc.2"
  id "org.aim42.htmlSanityCheck" version "0.9.3"
}

import com.monochromeroad.gradle.plugin.aws.s3.S3Sync

def siteDir = 'src/html'
def unavailableDir = 'build/unavailableSite'

def destBucket = 'www.parkyourprofits.com'
//def destBucket = 'unavailable.parkyourprofits.com'

bower {
    installBase = 'src/html/assets/bower'

    'jquery.countdown'()
}


githubPages {
  repoUri = 'https://github.com/parkyourprofits/parkyourprofits.github.io.git'
  targetBranch = 'master'

  pages {
    from unavailableDir
  }
}

task buildUnavailable(type: Copy) {
    from siteDir
    into unavailableDir

    // Use a closure to map the file name
    rename { String fileName ->
        switch (fileName) {
            case "index.html": return "unavailable.html"
            case "unavailable.html": return "index.html"
            default: return fileName
        }
    }
}

publishGhPages.dependsOn buildUnavailable

task publishS3(type: S3Sync) {
    description = "Publishes (synchronizes) \"${siteDir}\" to S3 bucket named \"${destBucket}\"."

    accessKey parkAwsAccessKey
    secretKey parkAwsSecretKey

    configFile "jets3t.properties"
    acl = com.monochromeroad.gradle.plugin.aws.s3.ACL.PublicRead

    from siteDir
    into destBucket
}

task publishGh(dependsOn: publishGhPages,
            description: "Publishes \"${siteDir}\" to Github Pages on 'targetBranch'")

task publish(dependsOn: [publishS3, publishGhPages],
            description: "Publishes \"${siteDir}\" to Github Pages and S3")


htmlSanityCheck {
    sourceDir = new File( "$siteDir" )
}
